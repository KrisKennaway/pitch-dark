;license:MIT
;(c) 2018 by 4am
;
; common assembler macros
;

PARAM    =     $00

!macro   PARAMS_ON_STACK .bytes {
         pla
         sta   PARAM
         plx
         stx   PARAM+1
         lda   #.bytes
         clc
         adc   PARAM
         bcc   +
         inx
+        phx
         pha
}

!macro   LDADDR .ptr {
         lda   #<.ptr
         ldy   #>.ptr
}

!macro   LDAY .ptr {
         lda   .ptr
         ldy   .ptr+1
}

!macro   STAY .ptr {
         sta   .ptr
         sty   .ptr+1
}

!macro   LDPARAM .offset {
         ldy   #.offset+1
         lda   (PARAM),y
         pha
         dey
         lda   (PARAM),y
         ply
}
