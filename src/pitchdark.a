;license:MIT
;(c) 2018 by 4am

!cpu 65c02
!ct "src/lcase.ct"
!to "build/PITCH.DARK",plain
*=$6000

         jmp   Start

         !source "src/macros.a"
         !source "src/constants.a"
         !source "src/WeeGUI_MLI.s"

         !source "src/games.a"
         !source "src/okvs.a"
         !source "src/prodos.a"
         !source "src/ramdisk.a"
         !source "src/path.a"
         !source "src/sound.a"
         !source "src/launch.a"
         !source "src/parse.common.a"
         !source "src/parse.gamelist.a"
         !source "src/parse.gameinfo.a"
         !source "src/parse.prefs.a"
         !source "src/ui.common.a"
         !source "src/ui.main.a"
         !source "src/ui.main.keys.a"
         !source "src/ui.options.a"

Start
         lda   MACHID
         and   #$30
         cmp   #$30                  ; 128K?
         beq   +                     ; yes, continue
-        jmp   QuitToProDOS
+        inc                         ; 65C02-only INC instruction will clear Z flag
         beq   -                     ; if Z flag is still set, this is not a 65C02

         jsr   LoadFile              ; load WEEGUI binary at $4000
         !word .weeguiFilename
         !word WGInit
         !word $2000
         !word kProDOSFileBuffer

         jsr   DisconnectRAM32       ; disconnect /RAM in S3,D2
         jsr   ClearInterpreterOptions ; clear options struct at $300
         jsr   WGInit                ; initialize WeeGUI
         jsr   LoadGameList          ; get master list of games
         jsr   LoadGlobalPreferences ; get current game and global options
         jsr   LoadGameInfo          ; get current game description and game-specific options
         ldx   #WGEnableMouse        ; enable mouse support
         jsr   WeeGUI
MainScreen
         ldx   #$FF
         txs
         ldx   #WGReset              ; reset WeeGUI views
         jsr   WeeGUI
         jsr   CreateViews           ; create all WeeGUI views (UI elements)
         ldx   #WGClearScreen        ; clear screen
         jsr   WeeGUI
         jsr   PaintAllViews         ; draw all UI elements
         bit   $C010                 ; clear keyboard strobe
.runLoop
         ldx   #WGPendingViewAction
         jsr   WeeGUI
         lda   $c000
         bpl   .runLoop
         bit   $c010
         jsr   HandleKey
         bra   .runLoop

ExitWeeGUI
         ldx   #WGDisableMouse       ; disable mouse support before quitting
         jsr   WeeGUI
         ldx   #WGExit               ; clean up WeeGUI
         jmp   WeeGUI

.weeguiFilename
         !byte 10
         !raw  "LIB/WEEGUI"

gGamesListStore
         !word *+2                 ; address first okvs store
