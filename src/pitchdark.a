;license:MIT
;(c) 2018 by 4am

!cpu 65c02
!ct "src/lcase.ct"
!to "build/PITCH.DARK",plain
*=$6000

         jmp   Start

         !source "src/macros.a"
         !source "src/constants.a"
         !source "src/WeeGUI_MLI.s"

         !source "src/gamelist.a"
         !source "src/okvs.a"
         !source "src/prodos.a"
         !source "src/ramdisk.a"
         !source "src/path.a"
         !source "src/infoparser.a"
         !source "src/prefs.a"
         !source "src/action.a"
         !source "src/uicommon.a"
         !source "src/mainscreen.a"
         !source "src/settingsdialog.a"
         !source "src/sound.a"

.weeguiFilename
         !byte 10
         !raw  "LIB/WEEGUI"

Start
         lda   MACHID
         and   #$30
         cmp   #$30                  ; 128K?
         beq   +                     ; yes, continue
-        jmp   QuitToProDOS
+        inc                         ; 65C02-only INC instruction will clear Z flag
         beq   -                     ; if Z flag is still set, this is not a 65C02

         jsr   DisconnectRAM32       ; disconnect /RAM in S3,D2 so we can use DHGR
         bit   $C010                 ; clear keyboard strobe

         jsr   LoadFile              ; load WEEGUI binary at $4000
         !word .weeguiFilename
         !word WGInit
         !word $2000
         !word kProDOSFileBuffer

         jsr   WGInit                ; initialize WeeGUI

         jsr   LoadGlobalPreferences ; get current game
         jsr   LoadGameInfo          ; load and parse game description text

         ldx   #WGEnableMouse        ; enable mouse support
         jsr   WeeGUI

MainScreen
         ldx   #$FF
         txs
         ldx   #WGReset              ; reset WeeGUI views
         jsr   WeeGUI
         jsr   CreateViews           ; create all WeeGUI views (UI elements)
         ldx   #WGClearScreen        ; clear screen
         jsr   WeeGUI
         jsr   PaintAllViews         ; draw all UI elements
.runLoop
         ldx   #WGPendingViewAction
         jsr   WeeGUI
         lda   $c000
         bpl   .runLoop
         bit   $c010
         jsr   HandleKey
         bra   .runLoop

ExitWeeGUI
         ldx   #WGDisableMouse       ; disable mouse support before quitting
         jsr   WeeGUI
         ldx   #WGExit               ; clean up WeeGUI
         jmp   WeeGUI

gPrefsStore
         !word *+2                 ; address of storage space for prefs
