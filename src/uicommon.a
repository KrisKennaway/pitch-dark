;license:MIT
;(c) 2018 by 4am
;
; User interface - common views and paint routines across screens
;
; Public functions
; - CreateRadio
; - CreateCheckbox
; - CreateButton
; - CreateTitleView
; - PaintTitleView
; - SetCheckboxByPref
; - SetPrefByCheckbox
;

; ROM routines
INVERSE        = $FE80
NORMAL         = $FE84

; View IDs (application-specific, acceptable range 0..15, no duplicates)
ID_TITLE       = 0

!zone {
CreateRadio
         ldx   #WGCreateRadio
         !byte $2C                   ; hide next 2 bytes
CreateCheckbox
         ldx   #WGCreateCheckbox
         !byte $2C                   ; hide next 2 bytes
CreateButton
         ldx   #WGCreateButton
         stx   .type

         +PARAMS_ON_STACK 2

         ldy   #$01
         lda   ($00),y
         sta   PARAM0
         iny
         lda   ($00),y
         sta   PARAM1
.type=*+1
         ldx   #$FD                  ; set at runtime
         jsr   WeeGUI
         ldx   #WGViewSetRawTitle
         lda   #1
         sta   PARAM0
         jmp   WeeGUI

CreateTitleView
         ldx   #WGCreateView         ; create title bar on top line
         lda   #<viewTitle
         sta   PARAM0
         lda   #>viewTitle
         sta   PARAM1
         jmp   WeeGUI

PaintTitleView
         ldx   #WGSelectView
         lda   #ID_TITLE
         jsr   WeeGUI
         jsr   INVERSE
         ldx   #WGPrint
         lda   #<.stringTitle
         sta   PARAM0
         lda   #>.stringTitle
         sta   PARAM1
         jsr   WeeGUI
         jmp   NORMAL

viewTitle
         !byte ID_TITLE              ; view ID
         !byte 0                     ; style
         !byte 0                     ; left
         !byte 0                     ; top
         !byte 80                    ; visible width
         !byte 1                     ; visible height
         !byte 80                    ; width
         !byte 1                     ; height
.stringTitle
         !raw  "                                   Pitch Dark                                   ",0
}

!zone {
SetCheckboxByPref
         +PARAMS_ON_STACK 3

         ldy   #1
         lda   (PARAM),y
         sta   .id
         iny
         lda   (PARAM),y
         sta   .key
         iny
         lda   (PARAM),y
         sta   .key+1

         jsr   okvs_get
         !word gPrefsStore
.key     !word $FDFD                 ; set at runtime
         bcs   .exit

         jsr   okvs_as_boolean
         beq   .exit

         ldx   #WGSelectView
.id=*+1
         lda   #$FD                  ; set at runtime
         jsr   WeeGUI
         ldx   #WGSetState
         lda   #1
         sta   PARAM0
         jsr   WeeGUI
.exit    rts
}

!zone {
SetPrefByCheckbox
         +PARAMS_ON_STACK 3

         ldy   #1
         lda   (PARAM),y
         sta   .id
         iny
         lda   (PARAM),y
         sta   .key
         iny
         lda   (PARAM),y
         sta   .key+1

         ldx   #WGSelectView
.id=*+1
         lda   #$FD                  ; set at runtime
         jsr   WeeGUI
         ldx   #WGGetState
         jsr   WeeGUI

         lda   PARAM0
         and   #1
         sta   .value+1

         jsr   okvs_update
         !word gPrefsStore
.key     !word $FDFD                 ; set at runtime
         !word .value
         rts
.value   !byte 1
         !byte $FD                   ; set at runtime
}
