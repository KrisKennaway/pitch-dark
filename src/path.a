;license:MIT
;(c) 2018 by 4am
;
; Functions to build a ProDOS pathname from length-prefixed strings
;
; Public functions
; - ResetPath
; - AddToPath
; - SetStartupPath
;

;------------------------------------------------------------------------------
; ResetPath
; reset gPathname to length 0
;
; in:    none
; out:   all registers preserved
;------------------------------------------------------------------------------
ResetPath
         stz   gPathname
         rts

;------------------------------------------------------------------------------
; AddToPath
; append a length-prefixed string to gPathname
;
; in:    A contains low byte of address of length-prefixed string to append
;        Y contains high byte of address of length-prefixed string to append
; out:   all registers and flags clobbered
;        $00/$01 clobbered
;        gPathname updated with concatenated length-prefixed string
;------------------------------------------------------------------------------
!zone {
AddToPath
         +STAY $00
         ldx   gPathname             ; current pathname length
         lda   ($00)                 ; length of this segment
         inc
         sta   .len
         ldy   #$01
-        lda   ($00),y
         sta   gPathname+1,x
         inx
         iny
.len=*+1
         cpy   #$FD                  ; SMC
         bcc   -
         stx   gPathname
         rts
}

;------------------------------------------------------------------------------
; SetStartupPath
; copy a length-prefixed string to $2006 (to pass it as the startup file when
; launching a .system file)
;
; in:    A contains low byte of address of length-prefixed string to append
;        Y contains high byte of address of length-prefixed string to append
; out:   all registers and flags clobbered
;        $00/$01 clobbered
;------------------------------------------------------------------------------
!zone {
SetStartupPath
         +STAY $00
         lda   ($00)
         tay
-        lda   ($00),y
         and   #$7F
         sta   $2006,y
         dey
         bpl   -
         rts
}

;------------------------------------------------------------------------------
; CreateNullTerminatedString
; Copy a length-prefixed string to kNullTerminatedBuffer and null-terminate it.
; Destination string is left-padded with a single space because reasons.
; Maximum length is 127 bytes.
;
; in:    A/Y contains address of length-prefixed string to copy
;        X contains length of null-terminated string -- if > length of source,
;          remaining buffer will be padded with spaces (#$A0)
; out:   X preserved
;        all other registers and flags clobbered
;        $00/$01 clobbered
;------------------------------------------------------------------------------
!zone {
CreateNullTerminatedString
         +STAY $00

         phx
         lda   #$A0
-        dex
         sta   kNullTerminatedBuffer,x
         bpl   -
         plx
         lda   #$00
         sta   kNullTerminatedBuffer,x

         lda   ($00)
         tay
-        lda   ($00),y
         sta   kNullTerminatedBuffer,y
         dey
         bne   -
         rts
}
